;NSIS Modern User Interface
;Start Menu Folder Selection Example Script
;Written by Joost Verburg

;--------------------------------
;Include Modern UI

  !include "MUI2.nsh"
  !include "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\FileAssociation.nsh"

;--------------------------------
;General

  ;Name and file
  Name "OpenTX companion"
  OutFile "companion-windows-v@VERSION@.exe"

  ;Default installation folder
  InstallDir "$PROGRAMFILES\OpenTX companion"
  
  ;Get installation folder from registry if available
  InstallDirRegKey HKCU "Software\companion9x" ""
  
  ;Compressor options
  SetCompressor /FINAL /SOLID lzma
  SetCompressorDictSize 64

  ;Request application privileges for Windows Vista
  RequestExecutionLevel admin

;--------------------------------
;Variables

  Var StartMenuFolder

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;Pages

  !insertmacro MUI_PAGE_LICENSE "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\license.txt"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY
  
  ;Start Menu Folder Page Configuration
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU" 
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\companion9x" 
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"
  
  !insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder
  
  !insertmacro MUI_PAGE_INSTFILES
  
  # These indented statements modify settings for MUI_PAGE_FINISH
    !define MUI_FINISHPAGE_NOAUTOCLOSE
    !define MUI_FINISHPAGE_RUN
    !define MUI_FINISHPAGE_RUN_CHECKED
    !define MUI_FINISHPAGE_RUN_TEXT "Launch companion"
    !define MUI_FINISHPAGE_RUN_FUNCTION "LaunchLink"
  #  !define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
  #  !define MUI_FINISHPAGE_SHOWREADME $INSTDIR\readme.txt
  !insertmacro MUI_PAGE_FINISH
  
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"
  !insertmacro MUI_LANGUAGE "French"

;--------------------------------
;Installer Sections

Section "OpenTX companion" SecDummy

  SetOutPath "$INSTDIR"
  
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\license.txt"
  File "companion.exe"
  File "C:\Programs\xerces\src\.libs\libxerces-c-3-1.dll"
  File "C:\MinGW\bin\libgcc_s_dw2-1.dll"
  File "C:\MinGW\bin\mingwm10.dll"
  File "C:\MinGW\bin\libstdc++-6.dll"
  File "@QT_BINARY_DIR@\QtCore4.dll"
  File "@QT_BINARY_DIR@\QtGui4.dll"
  File "@QT_BINARY_DIR@\QtXml4.dll"
  File "@QT_BINARY_DIR@\QtNetwork4.dll"
  File "@QT_BINARY_DIR@\Phonon4.dll"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\avrdude.exe"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\avrdude.conf"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\libusb0.dll"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\dfu-util.exe"
   
  CreateDirectory "$INSTDIR\phonon_backend"
  SetOutPath "$INSTDIR\phonon_backend"
  File "@QT_BINARY_DIR@\..\plugins\phonon_backend\Phonon_ds94.dll"
  
  CreateDirectory "$INSTDIR\lang"
  SetOutPath "$INSTDIR\lang"
  File "*.qm"
  
  SetOutPath "$INSTDIR"
  ;Store installation folder
  WriteRegStr HKCU "Software\companion9x" "" $INSTDIR
  
  ;Associate with extentions ,bin and .hex
  ${registerExtension} "$INSTDIR\companion.exe" ".bin" "BIN_File"
  ${registerExtension} "$INSTDIR\companion.exe" ".hex" "HEX_File"
  ${registerExtension} "$INSTDIR\companion.exe" ".companion" "EEPE_File"
  ${registerExtension} "$INSTDIR\companion.exe" ".eepm" "EEPM_File"
  
  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  ;Registry information for add/remove programs
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX" "DisplayName" "OpenTX Companion"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX" "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX" "DisplayIcon" "$\"$INSTDIR\companion.exe$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX" "Publisher" "OpenTX"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX" "URLInfoAbout" "http://www.open-tx.org"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\ImageMaker" "QuietUninstallString" "$\"$INSTDIR\Uninstall.exe$\" /S"

  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application
    
  ;Create shortcuts
  CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\companion.lnk" "$INSTDIR\companion.exe"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk" "$INSTDIR\Uninstall.exe"
  
  !insertmacro MUI_STARTMENU_WRITE_END

SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_SecDummy ${LANG_ENGLISH} "Models and settings editor for OpenTX"
  LangString DESC_SecDummy ${LANG_FRENCH} "Editeur de r�glages et mod�les pour OpenTX"

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${SecDummy} $(DESC_SecDummy)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END
 
;--------------------------------
;Uninstaller Section

Section "Uninstall"

  ;ADD YOUR OWN FILES HERE...
 
  Delete "$INSTDIR\license.txt"
  Delete "$INSTDIR\companion.exe"
  Delete "$INSTDIR\libxerces-c-3-1.dll"
  Delete "$INSTDIR\libgcc_s_dw2-1.dll"
  Delete "$INSTDIR\mingwm10.dll"
  Delete "$INSTDIR\libstdc++-6.dll"
  Delete "$INSTDIR\QtCore4.dll"
  Delete "$INSTDIR\QtGui4.dll"
  Delete "$INSTDIR\QtNetwork4.dll"
  Delete "$INSTDIR\Phonon4.dll"
  Delete "$INSTDIR\QtXml4.dll"
  Delete "$INSTDIR\avrdude.exe"
  Delete "$INSTDIR\avrdude.conf"
  Delete "$INSTDIR\libusb0.dll"
  Delete "$INSTDIR\dfu-util.exe"
  Delete "$INSTDIR\Uninstall.exe"
  
  Delete "$INSTDIR\lang\*.*"
  Delete "$INSTDIR\phonon_backend\*.*"

  RMDir "$INSTDIR\lang"
  RMDir "$INSTDIR\phonon_backend"
  RMDir "$INSTDIR"
  
  ${unregisterExtension} ".bin" "BIN File"
  ${unregisterExtension} ".hex" "HEX File"
  ${unregisterExtension} ".eepm" "EEPE File"
  ${unregisterExtension} ".companion" "EEPM File"
  
  !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder
    
  Delete "$SMPROGRAMS\$StartMenuFolder\companion.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall.lnk"
  RMDir "$SMPROGRAMS\$StartMenuFolder"
  
  DeleteRegKey /ifempty HKCU "Software\companion9x"
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX"

SectionEnd

Function LaunchLink
  ExecShell "" "$INSTDIR\companion.exe"
FunctionEnd
