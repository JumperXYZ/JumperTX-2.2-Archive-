;NSIS Modern User Interface
;Start Menu Folder Selection Example Script
;Written by Joost Verburg

;--------------------------------
;Include Modern UI

  !include "MUI2.nsh"
  !include "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\FileAssociation.nsh"

;--------------------------------
;General

  ;Name and file
  Name "OpenTX Companion @VERSION_FAMILY@"
  OutFile "companion-windows-@VERSION@.exe"

  ;Default installation folder
  InstallDir "$PROGRAMFILES\OpenTX\Companion @VERSION_FAMILY@"

  ;Get installation folder from registry if available
  InstallDirRegKey HKCU "Software\OpenTX\Companion @VERSION_FAMILY@" ""

  ;Compressor options
  SetCompressor /FINAL /SOLID lzma
  SetCompressorDictSize 64

  ;Request application privileges for Windows Vista
  RequestExecutionLevel admin

;--------------------------------
;Variables

  Var StartMenuFolder

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;File Exists Macro

; See http://nsis.sourceforge.net/Check_if_a_file_exists_at_compile_time for documentation
!macro !defineifexist _VAR_NAME _FILE_NAME
	!tempfile _TEMPFILE
	!ifdef NSIS_WIN32_MAKENSIS
		; Windows - cmd.exe
		!system 'if exist "${_FILE_NAME}" echo !define ${_VAR_NAME} > "${_TEMPFILE}"'
	!else
		; Posix - sh
		!system 'if [ -e "${_FILE_NAME}" ]; then echo "!define ${_VAR_NAME}" > "${_TEMPFILE}"; fi'
	!endif
	!include '${_TEMPFILE}'
	!delfile '${_TEMPFILE}'
	!undef _TEMPFILE
!macroend
!define !defineifexist "!insertmacro !defineifexist"

;--------------------------------
;Pages

  !insertmacro MUI_PAGE_LICENSE "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\license.txt"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY

  ;Start Menu Folder Page Configuration
  !define MUI_STARTMENUPAGE_REGISTRY_ROOT "HKCU"
  !define MUI_STARTMENUPAGE_REGISTRY_KEY "Software\OpenTX\Companion @VERSION_FAMILY@"
  !define MUI_STARTMENUPAGE_REGISTRY_VALUENAME "Start Menu Folder"

  !insertmacro MUI_PAGE_STARTMENU Application $StartMenuFolder

  !insertmacro MUI_PAGE_INSTFILES

  # These indented statements modify settings for MUI_PAGE_FINISH
    !define MUI_FINISHPAGE_NOAUTOCLOSE
    !define MUI_FINISHPAGE_RUN
    !define MUI_FINISHPAGE_RUN_CHECKED
    !define MUI_FINISHPAGE_RUN_FUNCTION "LaunchLink"
  #  !define MUI_FINISHPAGE_SHOWREADME_NOTCHECKED
  #  !define MUI_FINISHPAGE_SHOWREADME $INSTDIR\readme.txt
  !insertmacro MUI_PAGE_FINISH

  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_COMPONENTS
  !insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages

  !insertmacro MUI_LANGUAGE "English"
  !insertmacro MUI_LANGUAGE "French"

;--------------------------------
;Installer Sections

Section "OpenTX Companion @VERSION_FAMILY@" SecDummy

  Delete "$INSTDIR\opentx-*-simulator.dll"
  Delete "$INSTDIR\lang\*.*"
  RMDir  "$INSTDIR\lang"
  SetOutPath "$INSTDIR\platforms"
  File "@QT_DLL_DIR@\..\plugins\platforms\qwindows.dll"

  SetOutPath "$INSTDIR"

  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\license.txt"
  File "..\Release\companion.exe"
  File "..\Release\simulator.exe"
  File "..\Release\opentx-*-simulator.dll"
  ${!defineifexist} PTHREAD_WINDIR "C:\Programs\pthreads\Pre-built.2\lib\pthreadVC2.dll"
  !ifdef PTHREAD_WINDIR
  File "C:\Programs\pthreads\Pre-built.2\lib\pthreadVC2.dll"
  !else
  File "@SYSDIR@\system32\pthreadVC2.dll"
  !endif
  File "@SYSDIR@\msvcp140.dll"
  File "@SYSDIR@\vcruntime140.dll"
  File "@QT_DLL_DIR@\Qt5Core.dll"
  File "@QT_DLL_DIR@\Qt5Gui.dll"
  File "@QT_DLL_DIR@\Qt5Widgets.dll"
  File "@QT_DLL_DIR@\Qt5PrintSupport.dll"
  File "@QT_DLL_DIR@\Qt5Network.dll"
  File "@QT_DLL_DIR@\Qt5Multimedia.dll"
  File "@QT_DLL_DIR@\Qt5Svg.dll"
  File "@QT_DLL_DIR@\Qt5Xml.dll"
  ${!defineifexist} EAY_LIB_WINDIR "@EAY_LIB_DIR@\libeay32.dll"
  !ifdef EAY_LIB_WINDIR
  File "@EAY_LIB_DIR@\libeay32.dll"
  File "@EAY_LIB_DIR@\ssleay32.dll"
  !endif

  File "@SDL_DIR@\SDL.dll"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\avrdude.exe"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\avrdude.conf"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\libusb0.dll"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\dfu-util.exe"
  File "@CMAKE_CURRENT_SOURCE_DIR@\..\targets\windows\libusb-1.0.dll"

  SetOutPath "$INSTDIR\printsupport"
  File "@QT_DLL_DIR@\..\plugins\printsupport\windowsprintersupport.dll"

  SetOutPath "$INSTDIR\imageformats"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qjpeg.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qsvg.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qdds.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qgif.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qicns.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qico.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qtga.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qtiff.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qwbmp.dll"
  File "@QT_DLL_DIR@\..\plugins\imageformats\qwebp.dll"

  SetOutPath "$INSTDIR\mediaservice"
  File "@QT_DLL_DIR@\..\plugins\mediaservice\dsengine.dll"

  SetOutPath "$INSTDIR"
  ;Store installation folder
  WriteRegStr HKCU "Software\OpenTX\Companion @VERSION_FAMILY@" "" $INSTDIR

  ;Associate with extentions .otx, .bin (TODO: remove .bin)
  ${registerExtension} "$INSTDIR\companion.exe" ".bin" "BIN File"
  ${registerExtension} "$INSTDIR\companion.exe" ".otx" "OpenTX Settings File"

  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

  ;Registry information for add/remove programs
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion @VERSION_FAMILY@" "DisplayName" "OpenTX Companion @VERSION_FAMILY@"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion @VERSION_FAMILY@" "UninstallString" "$\"$INSTDIR\Uninstall.exe$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion @VERSION_FAMILY@" "DisplayIcon" "$\"$INSTDIR\companion.exe$\""
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion @VERSION_FAMILY@" "Publisher" "OpenTX"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion @VERSION_FAMILY@" "URLInfoAbout" "http://www.open-tx.org"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion @VERSION_FAMILY@" "QuietUninstallString" "$\"$INSTDIR\Uninstall.exe$\" /S"

  !insertmacro MUI_STARTMENU_WRITE_BEGIN Application

  ;Create shortcuts
  CreateDirectory "$SMPROGRAMS\$StartMenuFolder"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Companion @VERSION_FAMILY@.lnk" "$INSTDIR\companion.exe"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Firmware Simulator @VERSION_FAMILY@.lnk" "$INSTDIR\simulator.exe"
  CreateShortCut "$SMPROGRAMS\$StartMenuFolder\Uninstall Companion @VERSION_FAMILY@.lnk" "$INSTDIR\Uninstall.exe"

  !insertmacro MUI_STARTMENU_WRITE_END

SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_SecDummy ${LANG_ENGLISH} "Models and settings editor for OpenTX"
  LangString DESC_SecDummy ${LANG_FRENCH} "Editeur de r�glages et mod�les pour OpenTX"

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SecDummy} $(DESC_SecDummy)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Uninstaller Section

Section "un.OpenTX Companion @VERSION_FAMILY@"

  SectionIn RO  ; Not deselectable

  ;ADD YOUR OWN FILES HERE...

  Delete "$INSTDIR\license.txt"
  Delete "$INSTDIR\companion.exe"
  Delete "$INSTDIR\simulator.exe"
  Delete "$INSTDIR\opentx-*-simulator.dll"
  Delete "$INSTDIR\pthreadVC2.dll"
  Delete "$INSTDIR\SDL.dll"
  Delete "$INSTDIR\msvcp140.dll"
  Delete "$INSTDIR\vcruntime140.dll"
  Delete "$INSTDIR\Qt5Core.dll"
  Delete "$INSTDIR\Qt5Gui.dll"
  Delete "$INSTDIR\Qt5Widgets.dll"
  Delete "$INSTDIR\Qt5PrintSupport.dll"
  Delete "$INSTDIR\Qt5Network.dll"
  Delete "$INSTDIR\Qt5Multimedia.dll"
  Delete "$INSTDIR\Qt5Xml.dll"
  Delete "$INSTDIR\Qt5Svg.dll"
  Delete "$INSTDIR\avrdude.exe"
  Delete "$INSTDIR\avrdude.conf"
  Delete "$INSTDIR\libusb0.dll"
  Delete "$INSTDIR\dfu-util.exe"
  Delete "$INSTDIR\libusb-1.0.dll"
  Delete "$INSTDIR\libeay32.dll"
  Delete "$INSTDIR\ssleay32.dll"
  Delete "$INSTDIR\Uninstall.exe"

  Delete "$INSTDIR\lang\*.*"
  Delete "$INSTDIR\phonon_backend\*.*"
  Delete "$INSTDIR\platforms\*.*"
  Delete "$INSTDIR\printsupport\*.*"
  Delete "$INSTDIR\mediaservice\*.*"
  Delete "$INSTDIR\imageformats\*.*"

  RMDir "$INSTDIR\lang"
  RMDir "$INSTDIR\phonon_backend"
  RMDir "$INSTDIR\platforms"
  RMDir "$INSTDIR\printsupport"
  RMDir "$INSTDIR\mediaservice"
  RMDir "$INSTDIR\imageformats"
  RMDir "$INSTDIR"

  ${unregisterExtension} ".bin" "BIN File"
  ${unregisterExtension} ".hex" "HEX File"
  ${unregisterExtension} ".eepm" "EEPE File"
  ${unregisterExtension} ".otx" "OpenTX Settings File"

  !insertmacro MUI_STARTMENU_GETFOLDER Application $StartMenuFolder

  Delete "$SMPROGRAMS\$StartMenuFolder\Companion @VERSION_FAMILY@.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Firmware Simulator @VERSION_FAMILY@.lnk"
  Delete "$SMPROGRAMS\$StartMenuFolder\Uninstall Companion @VERSION_FAMILY@.lnk"
  RMDir "$SMPROGRAMS\$StartMenuFolder"

  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTX Companion @VERSION_FAMILY@"
SectionEnd

Section /o "un.Settings"
  DeleteRegKey HKCU "Software\OpenTX\Companion @VERSION_FAMILY@"
SectionEnd

Function LaunchLink
  ExecShell "" "$INSTDIR\companion.exe"
FunctionEnd
